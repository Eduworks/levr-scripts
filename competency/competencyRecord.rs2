//---------------------------------------
//---------------Helpers
//---------------------------------------
//==============

modelDB = {
	indexDir = #competencyModelDirectory(),
	databaseName = #competencyModelDBName(),
	index = "0"
};

userDB = {
	indexDir = #competencyUserDirectory(),
	databaseName = #competencyUserDBName(),
	index = "0"
};

recordDB = {
	indexDir = #competencyUserDirectory(),
	databaseName = "records",
	index="@userId"
};

firstChar = #string(str="@str", op="substr", begin="0", end="1");

removedFirst = #string(str="@str", op="substr", begin="1");
simpleId = #if(operator=firstChar, operand=":", eq=removedFirst, ne="@str");

//---------------------------------------
//---------------Service Functions
//---------------------------------------
//==============
// CREATE Competency Record
//--------------
// #competencyCreateRecord
//--------------
// userId:
// modelId:
// competencyId:
// levelId: 
//==============

// TODO: Check if the competency has a new version, if it does send error saying this is an older version of the competency (also add an override parameter to this so we can override the error if we want)

recordValues = #add(
	a="{':recordCompetency': ['",
	b="@competencyId", 
	c="'], ':recordLevel': ['",
	d="@levelId",
	e="'], ':recordUpdated': ['",
	f=#competencyNow(),
	g="'], ':recordCompetencyModel': ['", 
	h="@modelId",
	i="']}"
);
					

countExisting = #toArray(obj="@result").count();

createRecord = #object().putInObject(
							dest="@recordId",
							item=#idxSet(recordDB, key="@recordId", obj=#toObject(obj=recordValues))
						).ccall(
							recordId=#add(a=":", b=#generateUUID())
						);

createOrRead = #if(operator=#toObject(obj="@result").count(), operand="0",
					eq=createRecord,
					ne=#decode(obj="@result")
				);

checkForRecord = #if(operator=#decode(obj="@recordObj").getByParam(param=":recordCompetency"), operand="@competencyId",
						eq=#if(operator=#decode(obj="@recordObj").getByParam(param=":recordCompetencyModel"), operand="@modelId",
								eq=#if(operator=#decode(obj="@recordObj").getByParam(param=":recordLevel"), operand="@levelId",
										eq=#decode(obj="@recordObj"),
										ne="false",
									),
								ne="false"
							),
						ne="false"
					).ccall(
						recordObj=#idxGet(recordDB, key="@recordId")
					);

findExisting = #idxKeys(recordDB).cforEach(op=checkRecord, paramName="recordId").remove(item="false");

findAndCreate = findExisting.toParameter(op=createOrRead, paramName="result");

createRecord = #object(
	a=#competencyErrorUserExists(),
	b=#competencyErrorModelIdSet(),
	c=#competencyErrorModelExists(),
	d=#competencyErrorUserOrAgentReadAccess(),
	e=#competencyErrorUserOrAgentReadAccess(currentUser="@userId"),
	f=#competencyErrorCompetencyIdSet(),
	g=#competencyErrorCompetencyExists(),
	h=#competencyErrorLevelExists(),
	i=#competencyErrorLevelNotForCompetency(),
	k=findAndCreate
).cget(k="");
//j=#ontologyAddImport(directory = #competencyDirectory(), ontologyId=#competencyUserModel(), importId=#competencyGetModelUri()),


#competencyCreateRecord = createRecord;


//==============
// READ Competency Record
//--------------
// #competencyReadRecord
//--------------
// userId:
// recordId:
//==============

readRecord = #object().putInObject(
							dest="@recordId",
							item=#idxGet(recordDB, key="@recordId").decode()
						);

checkAccessRead = #object(
						a=#decode(obj="@record").getByParam(param="@recordId").getByParam(param=":recordCompetencyModel").toArray().getIndex(index="0").toParameter(op=#competencyErrorUserOrAgentReadAccess(), paramName="modelId"),
						b=#decode(obj="@record")
					).ccall(record=readRecord).cget(b="");

checkThenRead = #object(
							a=#competencyErrorUserIdSet(),
							b=#competencyErrorUserExists(),
							c=#competencyErrorRecordIdSet(),
							d=#competencyErrorRecordExists(),
							e=checkAccessRead
						).cget(e="");

#competencyReadRecord = checkThenRead;


//==============
// READ ALL Competency Records
//--------------
// #competencyReadAllRecords
//--------------
// userId:
//==============

getRecord = readRecord.getByParam(param="@recordId");

getRecords = #idxKeys(recordDB).cforEach(op=getRecord, paramName="recordId");

checkAccess = #if(operator=#competencyCheckUserOrAgentReadAccess(modelId=#decode(obj="@record").getByParam(param=":recordCompetencyModel").toArray().getIndex(index="0")), operand="true",
						eq=#decode(obj="@record"),
						ne=#competencyMsgUserOrAgentReadAccess()
				);

checkThenReadRecords = #object(
							a=#competencyErrorUserIdSet(),
							b=#competencyErrorUserExists(),
							c=getRecords.cforEach(op=checkAccess, paramName="recordId", valueName="record")
						).cget(c="");

#competencyReadAllRecords = checkThenReadRecords;


//==============
// UPDATE Competency Record Confidence
//--------------
// #competencyUpdateRecordConfidence
//--------------
// userId:
// recordId:
// confidence:
//==============
						
updateRecVals = #idxGet(recordDB, key="@recordId").putInObject(
	dest=":recordConfidence", 
	item=#toArray(obj="@confidence"),
).putInObject(
	dest=":recordUpdated",
	item=#toArray(obj=#competencyNow())
);

updateRec = #idxSet(recordDB, key="@recordId", obj=updateRecVals);
						
updateRecord = #object().putInObject(
							dest="@recordId",
							item=updateRec
						);

checkUpdateRecord = #object(
						a=#competencyErrorUserIdSet(),
						b=#competencyErrorUserExists(),
						c=#competencyErrorRecordIdSet(),
						d=#competencyErrorRecordExists(),
						e=#competencyReadRecord(),
						f=#competencyErrorConfidenceSet(),
						g=#competencyErrorConfidenceValue(),
						h=updateRecord
					).cget(h="");

#competencyUpdateRecordConfidence = checkUpdateRecord;

//==============
// DELETE Competency Record
//--------------
// #competencyDeleteRecord
//--------------
// userId:
// recordId:
//==============

deleteRecord = #idxDelete(recordDB, key="@recordId");

checkDeleteRecord = #object(
						a=#competencyErrorUserIdSet(),
						b=#competencyErrorUserExists(),
						c=#competencyErrorRecordIdSet(),
						d=#competencyErrorRecordExists(),
						e=#competencyReadRecord(),
						f=deleteRecord,
						g="true"
					).cget(g="");

#competencyDeleteRecord = checkDeleteRecord;


//==============
// READ User's Competencies
//--------------
// #competencyReadUserCompetencies
//--------------
// userId:
//==============

readUserCompParams = {
	directory = #competencyDirectory(),
	ontologyId = "@modelId",
	instanceId = "@competencyId"
};

readCompetency = #ontologyReadInstance(readUserCompParams).reduce();

readCompetencies = #if(operator=#competencyCheckUserOrAgentReadAccess(), operand="true", 
						ne=#competencyMsgUserOrAgentReadAccess(),
						eq=#ontologyReadInstance(readUserCompParams).ccall(competencyId=#toArray(obj="@competencyList"))
					);

readUserCompetencies = #competencyListUserCompetencies(userId="@userId").cforEach(op=readCompetencies, paramName="modelId", valueName="competencyList");

checkReadUserCompetencies = #object(
								a=#competencyErrorUserIdSet(),
								b=#competencyErrorUserExists(),
								c=readUserCompetencies
							).cget(c="");

#competencyReadUserCompetencies = checkReadUserCompetencies;
