decalsPostData=#getMultipartPostData(field="decalsData").toObject();

collectionDb={
	indexDir=#decalsCollectionDbDirectory(),
	databaseName=#decalsCollectionDbName(),
	index=#decalsCollectionDbIndexName() 
};

userDb={
	indexDir=#decalsUserDbDirectory(),
	databaseName=#decalsUserDbName(),
	index=#decalsUserDbIndexName() 
};

collectionObj=#idxGet(collectionDb,key="@collectionId").toObject();
allCollectionObj=#idxKeys(collectionDb).cforEach(paramName="collectionId",op=collectionObj);

invalidAccessError=#error(code="401",msg=#object(error="Invalid access for task"));

//------------------------------------
// Remove Group From All Collections

groupsForCollection=collectionObj.cget(groups="");

groupsForCollectionMinusGroup=groupsForCollection.cforEach(
	paramName="cgIt",
    op=#if(
    	operator=#toObject(obj="@cgIt").cget(groupId=""),
    	operand="@groupId",
    	ne=#toObject(obj="@cgIt")
    )   
).valueSet();

collectionHasGroup = groupsForCollection.cforEach(
	paramName="cgIt",
    op=#toObject(obj="@cgIt").cget(groupId="")
).valueSet().has(has="@groupId");

removeGroupFromAllCollections=allCollectionObj.cforEach(
	paramName="collectionId",
    op=#if(
    	operator=collectionHasGroup,
		operand="true",
		eq=collectionObj.putInObject(
			dest="groups",
			item=#listAdd(
				a=groupsForCollectionMinusGroup
			)
		).idxSet(collectionDb,key="@collectionId")
	)
);
#decalsRemoveGroupFromAllCollections = removeGroupFromAllCollections;

//============================
//Collections
//============================

newCollectionUserObj=#object(
	userId="@userId",
	firstName=#userGet(userDb,userId="@userId").cget(firstName=""),
	lastName=#userGet(userDb,userId="@userId").cget(lastName=""),
	access="@access"	
);

newCollectionItemObj=#object(	
	itemIndex="@itemIndex",
	resourceTitle="@resourceTitle",
	resourceUrl="@resourceUrl",
	resourceDescription="@resourceDescription"
);

itemsForCollection=collectionObj.cget(items="");

itemsForCollectionMinusItem=itemsForCollection.cforEach(
	paramName="ciIt",
    op=#if(
    	operator=#toObject(obj="@ciIt").cget(resourceUrl=""),
    	operand="@resourceUrl",
    	ne=#toObject(obj="@ciIt")
    )   
).valueSet();

collectionHasItem = itemsForCollection.cforEach(
	paramName="ciIt",
    op=#toObject(obj="@ciIt").cget(resourceUrl="")
).valueSet().has(has="@resourceUrl");

usersForCollection=collectionObj.cget(users="");

usersForCollectionMinusUser=usersForCollection.cforEach(
	paramName="cuIt",
    op=#if(
    	operator=#toObject(obj="@cuIt").cget(userId=""),
    	operand="@userId",
    	ne=#toObject(obj="@cuIt")
    )   
).valueSet();

collectionHasUser = usersForCollection.cforEach(
	paramName="cuIt",
    op=#toObject(obj="@cuIt").cget(userId="")
).valueSet().has(has="@userId");

collectionHasMemberGroup = #decalsUserMemberGroups().keySet().cforEach(
    	paramName="groupId",
    	op = collectionHasGroup.toString()
).valueSet().has(has="true");

userAccessForCollection = usersForCollection.cforEach(
	paramName="cuIt",
    op=#if(
    	operator=#toObject(obj="@cuIt").cget(userId=""),
    	operand="@userId",
    	eq=#toObject(obj="@cuIt").cget(access="")
    )
).valueSet().getIndex(index="0");

groupAccessForCollection = groupsForCollection.cforEach(
	paramName="cgIt",
    op=#if(
    	operator=#toObject(obj="@cgIt").cget(groupId=""),
    	operand="@groupId",
    	eq=#toObject(obj="@cgIt").cget(access="")
    )
).valueSet().getIndex(index="0");

userGroupAccessForCollection = #decalsUserMemberGroups().keySet().cforEach(
    	paramName="groupId",
    	op = groupAccessForCollection
);

userCanModifyCollection = #if(
	operator=userAccessForCollection,
	operand=#decalsModifyAccess(),
	eq="true",
	ne=#if(
		operator=userAccessForCollection,
		operand=#decalsViewAccess(),
		eq="false",
		ne=#if(
			operator=userGroupAccessForCollection.valueSet().has(has=#decalsModifyAccess()).toString(),
			operand="true",
			eq="true",
			ne="false"
		)
	)
);

checkHasCollectionModifyAccess = #if(	
	operator=userCanModifyCollection, 
	operand="false", 
	eq=invalidAccessError
);

//--------------------------
//Show all collections
//***DEBUG: COMMENT OUT OF PROD***

getAllCollections=allCollectionObj.displayJson();
/decalsShowAllCollections=getAllCollections;

//--------------------------
//Collections for user

userCollections=allCollectionObj.cforEach(
	paramName="collectionId",
    op=#if(
    	operator=collectionHasUser,
		operand="true",
		eq=collectionObj,
		ne=#if(
			operator=collectionHasMemberGroup,
			operand="true",
			eq=collectionObj
		)
	)
);

getCollectionsForUser=#object(
	a=#decalsCheckSession(sessionId=decalsPostData.cget(sessionId="")),
	b=userCollections.ccall(userId=decalsPostData.cget(userId=""))
).cget(b="").displayJson();

/decalsGetCollectionsForUser=getCollectionsForUser;

//--------------------------
//Get collection

getCollection=#object(
	a=#decalsCheckSession(sessionId=decalsPostData.cget(sessionId="")),
	b=collectionObj.ccall(collectionId=decalsPostData.cget(collectionId=""))
).cget(b="").displayJson();

/decalsGetCollection=getCollection;

//--------------------------
//Add collection

newCollectionObj=#object(
	collectionId="@collectionId",
	name="@collectionName",
	description="@collectionDescription",
	creator="@userId",
	createdDate=#date(_raw="true"),
	updatedDate=#date(_raw="true"),
	users=#listAdd(a=newCollectionUserObj.ccall(access=#decalsModifyAccess()))	
);

createCollection=#object(
	a=#decalsCheckSession(sessionId=decalsPostData.cget(sessionId="")),
	b=newCollectionObj.ccall(
		collectionName=decalsPostData.cget(collectionName=""),
		collectionDescription=decalsPostData.cget(collectionDescription=""),
		userId=#decalsSessionUserId(sessionId=decalsPostData.cget(sessionId=""))
	).idxSet(collectionDb,key="@collectionId"),
	c=collectionObj
).ccall(collectionId=#generateUUID()).cget(c="").displayJson();

/decalsCreateCollection = createCollection;

//--------------------------
//Delete collection

deleteCollection=#object(
	a=#decalsCheckSession(sessionId=decalsPostData.cget(sessionId="")),
	b=checkHasCollectionModifyAccess.ccall(userId=#decalsSessionUserId(sessionId=decalsPostData.cget(sessionId=""))),
	c=#idxDelete(collectionDb,key="@collectionId"),
	d=#object(msg="true")
).ccall(collectionId=decalsPostData.cget(collectionId="")).cget(d="").displayJson();

/decalsDeleteCollection = deleteCollection;

//--------------------------
//Add collection user

addCollectionUser=#object(
	a=#decalsCheckSession(sessionId=decalsPostData.cget(sessionId="")),
	b=checkHasCollectionModifyAccess.ccall(userId=#decalsSessionUserId(sessionId=decalsPostData.cget(sessionId=""))),
	c=collectionObj.putInObject(
		dest="users",
		item=#listAdd(
			a=usersForCollectionMinusUser.ccall(userId=decalsPostData.cget(userId="")),
			b=newCollectionUserObj.ccall(userId=decalsPostData.cget(userId=""),access=decalsPostData.cget(access=""))
		)
	).putInObject(
		dest="updatedDate",
		item=#date(_raw="true")
	).idxSet(collectionDb,key="@collectionId"),
	d=collectionObj
).ccall(collectionId=decalsPostData.cget(collectionId="")).cget(d="").displayJson();

/decalsAddCollectionUser = addCollectionUser;

//--------------------------
//Remove collection user

removeCollectionUser=#object(
	a=#decalsCheckSession(sessionId=decalsPostData.cget(sessionId="")),
	b=checkHasCollectionModifyAccess.ccall(userId=#decalsSessionUserId(sessionId=decalsPostData.cget(sessionId=""))),
	c=collectionObj.putInObject(
		dest="users",
		item=#listAdd(
			a=usersForCollectionMinusUser.ccall(userId=decalsPostData.cget(userId=""))
		)
	).putInObject(
		dest="updatedDate",
		item=#date(_raw="true")
	).idxSet(collectionDb,key="@collectionId"),
	d=collectionObj
).ccall(collectionId=decalsPostData.cget(collectionId="")).cget(d="").displayJson();

/decalsRemoveCollectionUser = removeCollectionUser;

//--------------------------
//Add collection item

addCollectionItem=#object(
	a=#decalsCheckSession(sessionId=decalsPostData.cget(sessionId="")),
	b=checkHasCollectionModifyAccess.ccall(userId=#decalsSessionUserId(sessionId=decalsPostData.cget(sessionId=""))),
	c=#decalsIncrementCollectionCount(resourceUrl=decalsPostData.cget(resourceUrl="")),
	d=collectionObj.putInObject(
		dest="items",
		item=#listAdd(
			a=itemsForCollectionMinusItem.ccall(resourceUrl=decalsPostData.cget(resourceUrl="")),
			b=newCollectionItemObj.ccall(
				resourceUrl=decalsPostData.cget(resourceUrl=""),
				resourceTitle=decalsPostData.cget(resourceTitle=""),
				resourceDescription=decalsPostData.cget(resourceDescription=""),
				itemIndex=#if(
					operator=decalsPostData.cget(itemIndex=""),
					operand="",
					ne=decalsPostData.cget(itemIndex=""),
					eq="999999"
				)
			)
		)
	).putInObject(
		dest="updatedDate",
		item=#date(_raw="true")
	).idxSet(collectionDb,key="@collectionId"),
	e=collectionObj
).ccall(collectionId=decalsPostData.cget(collectionId="")).cget(e="").displayJson();

/decalsAddCollectionItem = addCollectionItem;

//--------------------------
//Remove collection item

removeCollectionItem=#object(
	a=#decalsCheckSession(sessionId=decalsPostData.cget(sessionId="")),
	b=checkHasCollectionModifyAccess.ccall(userId=#decalsSessionUserId(sessionId=decalsPostData.cget(sessionId=""))),
	c=#decalsDecrementCollectionCount(resourceUrl=decalsPostData.cget(resourceUrl="")),
	d=collectionObj.putInObject(
		dest="items",
		item=#listAdd(
			a=itemsForCollectionMinusItem.ccall(resourceUrl=decalsPostData.cget(resourceUrl=""))
		)
	).putInObject(
		dest="updatedDate",
		item=#date(_raw="true")
	).idxSet(collectionDb,key="@collectionId"),
	e=collectionObj
).ccall(collectionId=decalsPostData.cget(collectionId="")).cget(e="").displayJson();

/decalsRemoveCollectionItem = removeCollectionItem;

//--------------------------
//Remove user from all collections

removeUserFromAllCollections=allCollectionObj.cforEach(
	paramName="collectionId",
    op=#if(
    	operator=collectionHasUser,
		operand="true",
		eq=collectionObj.putInObject(
			dest="users",
			item=#listAdd(
				a=usersForCollectionMinusUser
			)
		).idxSet(collectionDb,key="@collectionId")
	)
);
#decalsRemoveUserFromAllCollections = removeUserFromAllCollections;

//--------------------------
//Remove item from all collections

removeItemFromAllCollections=allCollectionObj.cforEach(
	paramName="collectionId",
    op=#if(
    	operator=collectionHasItem,
		operand="true",
		eq=collectionObj.putInObject(
			dest="items",
			item=#listAdd(
				a=itemsForCollectionMinusItem
			)
		).idxSet(collectionDb,key="@collectionId")
	)
);
#decalsRemoveItemFromAllCollections = removeItemFromAllCollections;

//--------------------------
//Update collection

checkNotInCollection = #if(operator=#cacheGet(obj="newResourceUrls").toArray().has(has="@resourceUrl"), operand="true",
							ne=#decalsDecrementCollectionCount().string(str="dec"),
							eq="nothing"
						);

checkInCollection = #if(operator=#cacheGet(obj="oldResourceUrls").toArray().has(has="@resourceUrl"), operand="true",
						ne=#decalsIncrementCollectionCount().string(str="inc"),
						eq="nothing"
					);

getURL = #decode(obj="@item").cget(resourceUrl="");

compareResources = #object(
						a=collectionObj.cget(items="").decode().cforEach(op=getURL, paramName="item", array="true").variableSet(key=#add(a="@ip", b="-oldResourceUrls")),
						b=decalsPostData.cget(collectionData="").cget(items="").decode().cforEach(op=getURL, paramName="item", array="true").variableSet(key=#add(a="@ip", b="-newResourceUrls")),
						c=#variableGet(key=#add(a="@ip", b="-newResourceUrls")).cforEach(op=checkInCollection, paramName="resourceUrl"),
						d=#variableGet(key=#add(a="@ip", b="-oldResourceUrls")).cforEach(op=checkNotInCollection, paramName="resourceUrl")
					);

updateCollectionDb=collectionObj.putInObject(
	dest="description",
	item=decalsPostData.cget(collectionData="").cget(description="")
).putInObject(
	dest="name",
	item=decalsPostData.cget(collectionData="").cget(name="")
).putInObject(
	dest="groups",
	item=decalsPostData.cget(collectionData="").cget(groups="")
).putInObject(
	dest="users",
	item=decalsPostData.cget(collectionData="").cget(users="")
).putInObject(
	dest="items",
	item=decalsPostData.cget(collectionData="").cget(items="")
).putInObject(
	dest="updatedDate",
	item=#date(_raw="true")
).idxSet(collectionDb,key="@collectionId");

updateCollection=#object(
	a=#decalsCheckSession(sessionId=decalsPostData.cget(sessionId="")),
	b=checkHasCollectionModifyAccess.ccall(userId=#decalsSessionUserId(sessionId=decalsPostData.cget(sessionId=""))),
	c=compareResources,
	d=updateCollectionDb,	
	z=collectionObj
).ccall(collectionId=decalsPostData.cget(collectionId="")).cget(z="").displayJson();

/decalsUpdateCollection = updateCollection;


